# Copyright (c) 2021 Oracle Corporation
# Licensed under the Universal Permissive License v 1.0
# as shown at https://oss.oracle.com/licenses/upl/

# Return the latest or all runnumber for some host/key
#

$include:"parameters.rwl"
$include:"results.rwl"
$longoption:quiet

string(100) mykey := $RWLOLTP_NAME;
$useroption:mykey

string(1000) myhostname := hostname;
$useroption:myhostname

integer showall:=0;
$userswitch:showall

string orderby:="desc";
string onlyone:="where rownum=1";

if showall then
  orderby:="asc";
  onlyone:=" ";
end if;


integer rno;

$ampersand:on
execute at rwloadsim
  for
    select runnumber rno from
    (
    select runnumber
    from rwlrun
    where key=:mykey
    and hostname=:myhostname
    order by rdate &orderby.
    )
    &onlyone.
  /
  loop
    printline rno;
  end loop;
  if rno is null then
    if showall then
      fprintf stderr,"No runs found for key=%s, hostname=%s\n", mykey, myhostname;
    else
      printline 0;
      fprintf stderr,"No run found for key=%s, hostname=%s\n", mykey, myhostname;
    end if;
    exit 1;
  end if;
end;

