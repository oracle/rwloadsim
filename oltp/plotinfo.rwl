# Copyright (c) 2024 Oracle Corporation
# Licensed under the Universal Permissive License v 1.0
# as shown at https://oss.oracle.com/licenses/upl/

# plotinfo.rwl
#
# Return some shell variables needed for oltpplot

# bengsig   3-jul-2024 - 0% dropped -> no dropped
# bengsig   9-may-2024 - Add drop%
# bengsig  19-mar-2024 - Better adtime check
# bengsig   5-mar-2024 - Creation

$include:"parameters.rwl"
$include:"results.rwl"
$longoption:quiet

integer myrunnumber := 0; $useroption:myrunnumber

execute at rwloadsim

  # did user run with $statsapptime:on and $statsdbtime:on ?
  double dtime, atime;
  for
    select dtime, atime from persec
    where runnumber = :myrunnumber
    and dtime is not null and atime is not null
    and dtime>0 and atime>0
    and vname = 'make_order'
    /
  loop
    break;
  end;

  if dtime is not null and dtime > 0 and atime is not null and atime > 0
  then
    printline "adtime=yes";
  else
    printline "adtime=no";
  end if;

  string forg;
  double fgcount, failcount, goodcount;
  for
    select decode(vname,'call_failure','FAIL','GOOD') forg, sum(ecount) fgcount
    from runres where runnumber = :myrunnumber
    and vname in
    ('call_failure'
    ,'make_order'
    ,'awindex_query'
    ,'aw_transaction'
    ,'query_order'
    ,'complex_query'
    )
    group by decode(vname,'call_failure','FAIL','GOOD')
    /
  loop
    if forg = "FAIL" then failcount := fgcount; end if;
    if forg = "GOOD" then goodcount := fgcount; end if;
  end loop;
  if failcount is not null and goodcount is not null then
    if failcount > 0.4 * goodcount then
      printf "droppct=%.0f%%\n", 100*failcount/goodcount;
    elseif failcount > 0.1 * goodcount then
      printf "droppct=%.1f%%\n", 100*failcount/goodcount;
    else
      printf "droppct=%.2f%%\n", 100*failcount/goodcount;
    end if;
  else
    printf "droppct=no\n";
  end if;

end;

