# sqlmonitor.rwl
#
# Copyright (c) 2022, 2024 Oracle Corporation
# Licensed under the Universal Permissive License v 1.0
# as shown at https://oss.oracle.com/licenses/upl/
#
# History
#
# bengsig  13-jun-2024 - modernize, call from shell script
# bengsig  27-jun-2023 - Fix _, mute
# bengsig  28-apr-2022 - Exit on connect error
# bengsig  13-apr-2022 - Change output's to use hyphen
# bengsig         2018 - Creation
#
#
# usage:
#
# rwloadsim -ul system/{password}@//host/database sqlmonitor.rwl sqlid
#
# output: sqlid.html
$alwaysmute:206

# If we didn't connect, don't do anything
$if not defined(default database) $then
  writeline stderr, "Missing or incorrect -l option; use -h to get help";
  exit 1;
$endif

$longoption:file-count=1

string sql_exec_start;

integer sql_exec_id;

integer sql_plan_hash_value;

string con_name;

string(4100) output_file;

string(4100) output_directory := "." ;

	              $userhelp:"Create a sql monitor by calling dbms_sql_monitor.report_sql_monitor"
	              $userhelp:"-l usr/pwd@con           : required option to give database logon"
	              $userhelp:"sqlid                    : exactly one sqlid must be given"
        $useroption:sql-exec-id:"--sql-exec-id=N          : provide sql_exec_id"
     $useroption:sql-exec-start:"--sql-exec-start="
                      $userhelp:"   YYYY.MM.DDTHH24:MI:SS : provide sql_exec_start"
$useroption:sql-plan-hash-value:"--sql-plan-hash-value=N  : provide sql_plan_hash_value"
           $useroption:con-name:"--con-name=con-name      : provide con_name"
        $useroption:output-file:"--output-file=filename   : write to filename in stead of sqlid.html"
   $useroption:output-directory:"--output-directory="
                      $userhelp:"   directory             : put file in directory in stead of current"

$oraerror:stop

string(13) sqlid;

if $# != 1 then
  writeline stderr, "\nusage: sqlmonitor [--help] [options] -l system/{password}@//host/database sqlid";
  exit 1;
end if;

sqlid := $1;

# $mute:46

string(10000000) txt;

sql rep_sql
  begin
  :1 := dbms_sql_monitor.report_sql_monitor
  ( sql_id => :2
  , sql_exec_id => :3
  , sql_exec_start => to_date(:4,'DD.MM.YYYY"T"HH24:MI:SS')
  , sql_plan_hash_value => :5
  , con_name => :6
  , type => 'active'
  , report_level => 'ALL');
  end;
  /
  bindout 1 txt;
  bind 2 sqlid, 3 sql_exec_id, 4 sql_exec_start;
  bind 5 sql_plan_hash_value, 6 con_name;
end;

rep_sql;

file out;

if output_file = "" then
  out := output_directory "/" sqlid || ".html";
else
  out := output_directory "/" output_file;
end if;


writeline out, txt;
out := null;
if output_file = "" then
  printline "Created " output_directory "/" sqlid ".html" ;
else
  printline "Created " output_directory "/" output_file;
end if;
