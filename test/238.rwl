# test return in procedure from inside cursor loop

integer debug238 := 0; $userswitch:debug238

$if debug238 $then
  $debugon:sql # used to inspect that return causes cursor cancel
$endif

$include:"testuserinfo.rwl"
$include:"testdefault.rwl"

string ename, dname;
integer deptno, empno;

# A simple query
sql getdepts
  select deptno, dname from dept order by deptno;
  define 2 dname, 1 deptno;
  array 10;
end;

# and another
sql getemps
  select empno, ename from emp where deptno=:1
  order by ename;
  define 1 empno, 2 ename;
  bind 1 deptno;
  array 10;
end;

# our test funtion
procedure testret(integer w)
  #
  integer d := 0, e := 0;
  for getdepts 
  loop
    d += 1;
    if w=1 and d=3 then
      $if debug238 $then printline "\e[7mreturn point 1\e[m"; $endif
      return;
    end if;
    e := 0;
    for getemps 
    loop
      e += 1;
      if w=2 and e=3 then
	$if debug238 $then printline "\e[7mreturn point 2\e[m"; $endif
        return;
      end if;
      printline empno, ename;
    end loop;
    printline deptno, dname;
  end loop;
  $if debug238 $then printline "\e[7mreturn point 3\e[m"; $endif
  return;
  printline "do not print this";
end;

testret(0);
testret(1);
testret(2);
